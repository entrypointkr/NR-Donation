plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    alias(libs.plugins.shadow)
}

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:${project.minecraft_version}-R0.1-SNAPSHOT")

    implementation("org.java-websocket:Java-WebSocket:1.5.6")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("plugin.yml") {
        expand "version": project.version,
                "spigot_api_version": project.spigot_api_version
    }
}

base {
    archivesBaseName = "${project.rootProject.name}"
    version = "${project.version}-spigot+mc${project.spigot_api_version}+"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks {
    runServer {
        minecraftVersion("${project.minecraft_version}")
    }
}

shadowJar {
    // 라이브러리 쉐이딩
    relocate 'org.java_websocket', 'net.teujaem.nrDonation.shaded.java_websocket'

    // 기본 jar와 구분되는 파일명 설정
    archiveClassifier.set('all')
}

jar {
    // 일반 JAR 활성화
    enabled = true
}
