plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

base {
    archivesBaseName = "${project.rootProject.name}"
    version = "${project.version}-fabric+mc${project.minecraft_version}"
}

repositories {
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
    maven { url = "https://api.modrinth.com/maven" }
}

dependencies {
    // ===== Fabric 기본 =====
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // ===== 외부 라이브러리 =====
    implementation(include(libs.gson.get()))
    implementation(include(libs.java.websocket.get()))
    implementation(include(libs.socket.io.client.get()))
    implementation(include(libs.engine.io.client.get()))
    implementation(include(libs.json.get()))
    implementation(include(libs.kotlin.stdlib.get()))
    implementation(include(libs.okio.get()))
    implementation(include(libs.okhttp.get()))
    implementation(include(libs.okhttpSse.get()))
    implementation(include(libs.snakeyaml.get()))
    implementation(include(libs.jackson.annotations.get()))
    implementation(include(libs.jackson.databind.get()))
    implementation(include(libs.jackson.core.get()))
    implementation(include(project(":common")))
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }

    filesMatching("nr-donation.client.mixins.json") {
        expand "java_version": project.java_version
    }

    filesMatching("nr-donation.mixins.json") {
        expand "java_version": project.java_version
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

Integer gradleJavaVersion = project.java_version as Integer
def targetJavaVersion = gradleJavaVersion
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.rootProject.name
            from components.java
        }
    }
}

def versionDir = "src/v${project.minecraft_version.replace('.', '_')}/java"

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", versionDir]
        }
        resources {
            srcDir "src/main/resources"
        }
    }
}
