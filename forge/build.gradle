plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    alias(libs.plugins.shadow)
}

base {
    archivesName = "${project.rootProject.name}"
    version = "${project.version}-forge+mc${project.forge_mc_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(project.forge_java_version)
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
        }

        client {
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', project.rootProject.name
        }

        server {
            property 'forge.enabledGameTestNamespaces', project.rootProject.name
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', project.rootProject.name
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', project.rootProject.name, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_mc_version}-${forge_version}"

    // ===== 외부 라이브러리 =====
    shadow(libs.gson)
    shadow(libs.java.websocket)
    shadow(libs.socket.io.client)
    shadow(libs.engine.io.client)
    shadow(libs.json)
    shadow(libs.kotlin.stdlib)
    shadow(libs.okio)
    shadow(libs.okhttp)
    shadow(libs.okhttpSse)
    shadow(libs.snakeyaml)
    shadow(libs.jackson.annotations)
    shadow(libs.jackson.databind)
    shadow(libs.jackson.core)
    shadow(project(":common"))
}

tasks.shadowJar {
    configurations.add(project.configurations.shadow)
    exclude('META-INF/**')
    relocate("org.slf4j", "shadow.slf4j")
    finalizedBy 'reobfShadowJar'
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : forge_mc_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : project.rootProject.name, mod_name: project.rootProject.name, mod_license: mod_license, mod_version: project.version,
                             mod_authors         : mod_authors, mod_description: mod_description,
                             description         : project.ext.description, version: version]

    inputs.properties replaceProperties

    filesMatching(['mcmod.info']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : project.rootProject.name,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }
    finalizedBy 'reobfJar'
}

reobf {
    create('shadowJar')
}

assemble.dependsOn(shadowJar)

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
